//==================================================================================================
// PROJECT
//==================================================================================================
version=project.hasProperty("version")?project.property("version"):"1.0.0"
group=project.group
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0'
        // BINTRAY
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
        // COVERALLS
        classpath 'net.saliman:gradle-cobertura-plugin:2.3.1'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3'
    }
}
task projectInfo << {
    println "\n Compiling: ${project.group}:${project.name}:${version} - (${System.getenv("CIRCLE_BUILD_NUM")})\n"
}
//==================================================================================================
// ANDROID
//==================================================================================================
apply plugin: 'com.android.library'
android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 23
        versionCode System.getenv("CIRCLE_BUILD_NUM")?System.getenv("CIRCLE_BUILD_NUM").toInteger():1
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // JVM tests
    testCompile 'junit:junit:4.12'
}
//==================================================================================================
// COVERALLS
//==================================================================================================
apply plugin: "net.saliman.cobertura"
apply plugin: "com.github.kt3k.coveralls"
cobertura.coverageFormats = ['html', 'xml']
//==================================================================================================
// BINTRAY
//==================================================================================================
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name project.name
                description "Tester android library with aiutomate deployment"
                licenses {
                    license {
                        name "MIT"
                        url "https://opensource.org/licenses/MIT"
                    }
                }
                developers {
                    developer {
                        id "cerberillo"
                        name "David Martin"
                        email "david@martinmacias.com"
                    }
                }
                scm {
                    connection "https://github.com/cerberillo/android-library-test.git"
                    developerConnection "https://github.com/cerberillo/android-library-test.git"
                    url "https://github.com/cerberillo/android-library-test"
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
String getRandomString (int size) {
    def alphabet = (('a'..'z') + ('0'..'9')).join()
    return new Random().with {(1..size).collect { alphabet[nextInt(alphabet.length())] }.join()}
}
bintray {

    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_API_KEY")

    pkg {
        configurations = ['archives']
        repo = 'maven'
        name = 'android-library-test'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/cerberillo/android-library-test.git'
        version {
            name = project.version
            vcsTag = project.version
        }
    }
}
