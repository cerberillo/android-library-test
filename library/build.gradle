def sharedVersion = "1.0.1"
def sharedBuild = 2
def sharedGroup="com.cerberillo"
//==================================================================================================
// PROJECT
//==================================================================================================
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0'
        // BINTRAY
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
        // COVERALLS
        classpath 'net.saliman:gradle-cobertura-plugin:2.3.1'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3'
    }
}
//==================================================================================================
// ANDROID
//==================================================================================================
apply plugin: 'com.android.library'
android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 23
        versionCode sharedBuild
        versionName sharedVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // JVM tests
    testCompile 'junit:junit:4.12'
}
//==================================================================================================
// COVERALLS
//==================================================================================================
apply plugin: "net.saliman.cobertura"
apply plugin: "com.github.kt3k.coveralls"
cobertura.coverageFormats = ['html', 'xml']

//==================================================================================================
// BINTRAY
//==================================================================================================
apply plugin: 'com.jfrog.bintray'
group = sharedGroup
version = sharedVersion
bintray {

    def bintrayUser = System.getenv("BINTRAY_USER");
    def bintrayApiKey = System.getenv("BINTRAY_API_KEY");

    File localPropertiesFile = project.rootProject.file('local.properties')
    if (localPropertiesFile.canRead()) {
        Properties properties = new Properties()
        properties.load(localPropertiesFile.newDataInputStream())
        user = properties.getProperty('bintray.user')
        key = properties.getProperty('bintray.api_key')
    } else {
        user = bintrayUser
        key = bintrayApiKey
    }

    pkg {
        repo = 'maven'
        name = 'android-library-test'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/cerberillo/android-library-test.git'
        version {
            name = sharedVersion
            vcsTag = sharedVersion
        }
    }
}